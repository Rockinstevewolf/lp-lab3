#№ Отчет по лабораторной работе №3
## по курсу "Логическое программирование"

## Решение задач методом поиска в пространстве состояний

### студент: Баранников С.А.

## Результат проверки

| Преподаватель     | Дата         |  Оценка       |
|-------------------|--------------|---------------|
| Сошников Д.В. |              |               |
| Левинская М.А.|   14/12      |       4       |

> *Комментарии проверяющих (обратите внимание, что более подробные комментарии возможны непосредственно в репозитории по тексту программы)*


## Введение

Какие задачи удобным образом решаются методом поиска в пространстве состояний? 
Почему Prolog оказывается удобным языком для решения таких задач?

## Задание

4. "Расстановка мебели". Площадь разделена на шесть квадратов, пять из них заняты мебелью, шестой свободен. Переставить мебель так, чтобы шкаф и кресло поменялись местами, при этом никакие два предмета не могут стоять на одном квадрате.

## Принцип решения

Опишите своими словами принцип решения задачи, приведите важные фрагменты кода. Какие алгоритмы поиска вы использовали? 

Для начала, потребовалось определить ребра передвижений в графе 2 на 3, то есть в графе из 6 узлов, имеющие только вертикальные и горизонтальные связи

| table  | chair  | wardrobe  |
| :------------: | :------------: | :------------: |
|  stool | empty  | lounge  |

```prolog
move(X, Y):- step(X, Y).
move(X, Y):- step(Y, X).

step([empty, X, A, B, C, D], [X, empty, A, B, C, D]).
step([A, empty, X, B, C, D], [A, X, empty, B, C, D]).
step([A, B, C, empty, X, D], [A, B, C, X, empty, D]).
step([A, B, C, D, empty, X], [A, B, C, D, X, empty]).

step([empty, A, B, X, C, D], [X, A, B, empty, C, D]).
step([A, empty, B, C, X, D], [A, X, B, C, empty, D]).
step([A, B, empty, C, D, X], [A, B, X, C, D, empty]).
```
где `step/2` - это возможные перемещения в зависимости от нахождения пустой ячейки в одну сторону,
а `move/2` - удвоение перемещений `step/2`, то есть добавление переходов состояний в обратную сторону.

Далее, с помощью поиска в ширину был найден минимальный пусть из начального состояния в итоговое:

Вспомогательные предикаты
```Prolog
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%Печать пути
print_ans([]).
print_ans([H|T]):- print_ans(T), print_one(H).

print_one([A,B,C,D,E,F]):-
    write('------------------------------------'), nl,
    write(A), write('   '),
    write(B), write('   '),
    write(C), write('   '), nl,
    write(D), write('   '),
    write(E), write('   '),
    write(F), write('   '), nl,
    write('------------------------------------'), nl.
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
prolong([X | T], [Y, X | T]):-
    move(X, Y),
    not(member(Y, [X | T])).
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
```
1. Поиск в ширину
```Prolog
%В случае если X находится в начале очереди, получает состояние, где содержится X
path([[X | T] | _], X, [X | T]).

%Ищет все пути из текущего состояния и производит слияние этих путей с текущей очередью, после чего продолжает с новым состоянием в очереди
path([P | Q], B, R):-
    findall(X, prolong(P, X), L),
    append(Q, L, QL), !,
    path(QL, B, R).

%Перенаправляющее состояние, которое происходит, если не достигается конечное состояние B, то есть отправляет в хвост списка (очередь)
path([_ | Q], B, R):- path(Q, B, R).

%Поиск в ширину, где A - начальное условие, а B - конечное, R - реверсивный список состояний
bdth(A, B, R):- path([[A]], B, R).
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
```
2. Поиск в глубину
```Prolog
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%Поиск в глубину
dpth([X|T], X, [X|T]).
dpth(P, F, L):- prolong(P, P1), dpth(P1, F, L).
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
```
3. Поиск в глубину с итеративным погружением
```Prolog
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Поиск в глубину с итеративным погружением
int(1).
int(X):-
    int(Y),
    X is Y + 1.

depth_id([Finish|T],Finish,[Finish|T],0).
depth_id(Path,Finish,R,N):-
    N > 0,
    prolong(Path,NewPath),
    N1 is N - 1,
    depth_id(NewPath,Finish,R,N1).

search_id(Start,Finish,Path):-
    int(Level),
    depth_id(Start,Finish,Path,Level).
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
```
4. Решатели требуемой задачи
```Prolog
solve_bdth:-
    BEGIN = [table, chair, wardrobe, stool, empty, lounge],
    AIM = [_, _, lounge, _, _, wardrobe],
    write('Начало: '), nl,
    print_one(BEGIN),
    write('Цель:'), nl,
    print_one(AIM),

    write('     Поиск в ширину...'), nl,
    get_time(T1),
    bdth(BEGIN, AIM, R),
    get_time(T2),
    write('Ходы: '), nl,
    print_ans(R),
    length(R, Len),
    write('Длина пути: '), Length is Len-1, write(Length), nl,
    write('Время поиска в ширину: '),
    T is T2-T1,
    write(T), nl.

solve_dpth:-
    BEGIN = [table, chair, wardrobe, stool, empty, lounge],
    AIM = [_, _, lounge, _, _, wardrobe],
    write('Начало: '), nl,
    print_one(BEGIN),
    write('Цель:'), nl,
    print_one(AIM),

    write('     Поиск в глубину...'), nl,
    get_time(T1),
    dpth([BEGIN], AIM, L),
    get_time(T2),
    write('Ходы: '), nl,
    print_ans(L),
    T is T2 - T1,
    length(L, Len),
    write('Длина пути: '), Length is Len-1, write(Length), nl,
    write('Время работы поиска в глубину: '), write(T), nl.

solve_dpth_id:-
    BEGIN = [table, chair, wardrobe, stool, empty, lounge],
    AIM = [_, _, lounge, _, _, wardrobe],
    int(DepthLimit),
    get_time(T1),
    depth_id([BEGIN],AIM,Res,DepthLimit),
    get_time(T2),
    write('Ходы: '), nl,
    print_ans(Res),
    T is T2 - T1,
    length(Res, Len),
    write('Длина пути: '), Length is Len-1, write(Length), nl,
    write('Максимальный лимит глубины: '), write(DepthLimit), nl,
    write('Время работы поиска в глугину с итеративным погружением: '), write(T), nl.
```

## Результаты

Приведите результаты работы программы: найденные пути, время, затраченное на поиск тем или иным алгоритмом, длину найденного первым пути. Используйте таблицы,
если необходимо.

| Алгоритм поиска |  Длина найденного первым пути  |  Время работы  |
|-----------------|--------------------------------|----------------|
| В глубину       |183             |0.0023374557495117188           |
| В ширину        |17              |0.02742147445678711             |
| ID              |17              |0.0004055500030517578           |

### Поиск в глубину
```Prolog
?- solve_dpth.
Начало:
------------------------------------
table   chair   wardrobe
stool   empty   lounge
------------------------------------
Цель:
------------------------------------
_582   _588   lounge
_600   _606   wardrobe
------------------------------------
     Поиск в глубину...
Ходы:
------------------------------------
table   chair   wardrobe
stool   empty   lounge
------------------------------------
------------------------------------
table   chair   wardrobe
stool   lounge   empty
------------------------------------
------------------------------------
table   chair   empty
stool   lounge   wardrobe
------------------------------------
------------------------------------
table   empty   chair
stool   lounge   wardrobe
------------------------------------
------------------------------------
table   lounge   chair
stool   empty   wardrobe
------------------------------------
------------------------------------
table   lounge   chair
stool   wardrobe   empty
------------------------------------

			
			.	.	. too much moves...


------------------------------------
empty   lounge   wardrobe
chair   stool   table
------------------------------------
------------------------------------
chair   lounge   wardrobe
empty   stool   table
------------------------------------
------------------------------------
chair   lounge   wardrobe
stool   empty   table
------------------------------------
------------------------------------
chair   lounge   wardrobe
stool   table   empty
------------------------------------
------------------------------------
chair   lounge   empty
stool   table   wardrobe
------------------------------------
------------------------------------
chair   empty   lounge
stool   table   wardrobe
------------------------------------
Длина пути: 183
Время работы поиска в глубину: 0.0023374557495117188
true
```
### Поиск в ширину
```Prolog
?- solve_bdth.
Начало:
------------------------------------
table   chair   wardrobe
stool   empty   lounge
------------------------------------
Цель:
------------------------------------
_7406   _7412   lounge
_7424   _7430   wardrobe
------------------------------------
     Поиск в ширину...
Ходы:
------------------------------------
table   chair   wardrobe
stool   empty   lounge
------------------------------------
------------------------------------
table   chair   wardrobe
stool   lounge   empty
------------------------------------
------------------------------------
table   chair   empty
stool   lounge   wardrobe
------------------------------------
------------------------------------
table   empty   chair
stool   lounge   wardrobe
------------------------------------
------------------------------------
table   lounge   chair
stool   empty   wardrobe
------------------------------------
------------------------------------
table   lounge   chair
empty   stool   wardrobe
------------------------------------
------------------------------------
empty   lounge   chair
table   stool   wardrobe
------------------------------------
------------------------------------
lounge   empty   chair
table   stool   wardrobe
------------------------------------
------------------------------------
lounge   chair   empty
table   stool   wardrobe
------------------------------------
------------------------------------
lounge   chair   wardrobe
table   stool   empty
------------------------------------
------------------------------------
lounge   chair   wardrobe
table   empty   stool
------------------------------------
------------------------------------
lounge   empty   wardrobe
table   chair   stool
------------------------------------
------------------------------------
empty   lounge   wardrobe
table   chair   stool
------------------------------------
------------------------------------
table   lounge   wardrobe
empty   chair   stool
------------------------------------
------------------------------------
table   lounge   wardrobe
chair   empty   stool
------------------------------------
------------------------------------
table   lounge   wardrobe
chair   stool   empty
------------------------------------
------------------------------------
table   lounge   empty
chair   stool   wardrobe
------------------------------------
------------------------------------
table   empty   lounge
chair   stool   wardrobe
------------------------------------
Длина пути: 17
Время поиска в ширину: 0.02742147445678711
```

### Поиск в глубину с итеративным погружением
```Prolog
?- solve_dpth_id.
Ходы:
------------------------------------
table   chair   wardrobe
stool   empty   lounge
------------------------------------
------------------------------------
table   chair   wardrobe
stool   lounge   empty
------------------------------------
------------------------------------
table   chair   empty
stool   lounge   wardrobe
------------------------------------
------------------------------------
table   empty   chair
stool   lounge   wardrobe
------------------------------------
------------------------------------
table   lounge   chair
stool   empty   wardrobe
------------------------------------
------------------------------------
table   lounge   chair
empty   stool   wardrobe
------------------------------------
------------------------------------
empty   lounge   chair
table   stool   wardrobe
------------------------------------
------------------------------------
lounge   empty   chair
table   stool   wardrobe
------------------------------------
------------------------------------
lounge   chair   empty
table   stool   wardrobe
------------------------------------
------------------------------------
lounge   chair   wardrobe
table   stool   empty
------------------------------------
------------------------------------
lounge   chair   wardrobe
table   empty   stool
------------------------------------
------------------------------------
lounge   empty   wardrobe
table   chair   stool
------------------------------------
------------------------------------
empty   lounge   wardrobe
table   chair   stool
------------------------------------
------------------------------------
table   lounge   wardrobe
empty   chair   stool
------------------------------------
------------------------------------
table   lounge   wardrobe
chair   empty   stool
------------------------------------
------------------------------------
table   lounge   wardrobe
chair   stool   empty
------------------------------------
------------------------------------
table   lounge   empty
chair   stool   wardrobe
------------------------------------
------------------------------------
table   empty   lounge
chair   stool   wardrobe
------------------------------------
Длина пути: 17
Максимальный лимит глубины: 17
Время работы поиска в глугину с итеративным погружением: 0.0004055500030517578
true
```
## Выводы

Очень интересная лабораторная работа с довольно сложными алгоритмами для решения задач. Пусть даже эти методы давно известны и их можно "нагуглить", однако они очень нетривиальны, чтобы их реализовыать самостоятельно. 

Наиболее оптимальным оказался `алгоритм поиска в глубину с итеративным погружением`, которому требуется `0.0004055500030517578` секунд, дабы решить данную задачу в `17` ходов.
Тот же `алгоритм поиска в ширину` так же совершил `17` ходов, но за `0.02742147445678711` секунд
Однако же `алгоритм поиска в глубину`, которому потребовалось `0.0023374557495117188` секунд пусть и на `183` хода, что оказывается быстрее `алгоритма поиска в ширину`, но не быстрее `алгоритма поиска в глубину с итеративным погружением`.
